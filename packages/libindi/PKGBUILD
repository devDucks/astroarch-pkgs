# Maintainer: Mattia Procopio (astro.matto)  <matto.astro at gmail dot com>
pkgname=libindi
pkgver=2.1.8
pkgrel=1
pkgdesc='Indi core library'
arch=(x86_64 aarch64)
url='https://github.com/indilib/indi'
license=(LGPLv2)
depends=(curl
         fftw
         gpsd
         gsl
         libev
         libftdi
         libgphoto2
         libjpeg-turbo
         libnova
         libraw
         libtheora
         libtiff	 
         libusb
         libuv
         rtl-sdr
         zlib)
makedepends=(boost
             cmake)
source=(https://github.com/indilib/indi/archive/refs/tags/v$pkgver.tar.gz)
sha256sums=('2beeaf18dcf418c84559958b63cc5e03bd095adf783cf7cc24b0b9d5be0de894')

build() {
  cmake -B $srcdir/build -DCMAKE_INSTALL_PREFIX=/usr \
	-DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    	-DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
	-DCMAKE_CXX_FLAGS="$CXX_FLAGS -ffat-lto-objects -Wp,-U_GLIBCXX_ASSERTIONS" \
    	indi-$pkgver
  cmake --build $srcdir/build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
