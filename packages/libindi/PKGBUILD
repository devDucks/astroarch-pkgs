# Maintainer: Mattia Procopio (astro.matto)  <matto.astro at gmail dot com>
pkgname=libindi
pkgver=2.0.2
pkgrel=1
pkgdesc="Indi core library"
arch=(x86_64 aarch64)
url="https://github.com/indilib/indi"
license=('LGPLv2')
depends=('gpsd' 'gsl' 'libraw' 'libusb' 'zlib' 'libftdi' 'libjpeg-turbo' 'libuv' \
         'libnova' 'libtiff' 'fftw' 'rtl-sdr' 'libgphoto2' 'curl' 'libtheora' \
	 'libev')
makedepends=('cmake' 'boost')
source=("https://github.com/indilib/indi/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('dae768e461a8f36988afa90e009768fb21729d105a2327939b2ffb05afb70035')

build() {
  cmake -B $srcdir/build -DCMAKE_INSTALL_PREFIX=/usr \
	-DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    	-DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
	-DCMAKE_CXX_FLAGS="$CXX_FLAGS -ffat-lto-objects -Wp,-U_GLIBCXX_ASSERTIONS" \
    	indi-$pkgver
  cmake --build $srcdir/build -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
