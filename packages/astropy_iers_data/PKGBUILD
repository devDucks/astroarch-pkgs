# Maintainer:

_pyname=astropy-iers-data
pkgname=python-$_pyname
pkgver=0.2024.02.05.00.30.52
pkgrel=2
pkgdesc="IERS Earth Rotation and Leap Second tables for the astropy core package"
arch=(any)
url="https://github.com/astropy/astropy-iers-data"
license=(BSD-3-Clause)
depends=(python)
makedepends=(python-build python-installer python-wheel python-setuptools-scm)
source=(https://github.com/astropy/$_pyname/archive/v$pkgver/$_pyname-$pkgver.tar.gz)
sha256sums=('42e65897f36741bd3ed08c0fa349fd25f370589f109e06d5fd5e33ef581bc85c')

build() {
  cd $_pyname-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver} \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mkdir -p "$pkgdir"/usr/lib/python3.11/site-packages/astropy_iers_data/data
  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm644 astropy_iers_data/data/* "$pkgdir"/usr/lib/python3.11/site-packages/astropy_iers_data/data
}
