# Maintainer:

_pyname=astropy-iers-data
pkgname=python-$_pyname
_pypi_name=astropy_iers_data
pkgver=0.2025.10.13.0.37.17
pkgrel=1
pkgdesc="IERS Earth Rotation and Leap Second tables for the astropy core package"
arch=(any)
url="https://github.com/astropy/astropy-iers-data"
license=(BSD-3-Clause)
depends=(python)
makedepends=(python-build python-installer python-wheel python-setuptools-scm python-hatchling python-hatch-vcs)
source=(https://pypi.org/packages/source/a/astropy-iers-data/astropy_iers_data-$pkgver.tar.gz)
sha256sums=('c1ecc6a8fc407e2388a309490a28bddfea6e049269ce490607218dab97cd0df6')

build() {
  cd $_pypi_name-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver} \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd $_pypi_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname
}
