# Maintainer: Ed Lee <ed@thefamilee.co.uk>
# Previous maintainer: Mattia Procopio <matto.astro@gmail.com>
# Original AUR PKGBUILD mantainer Daniel Reibl <janos.daniel.reibl@pm.me>
# Most changes from previous work thanks to StÃ©phane Carlin
# This build contains some changes that allow FireCapture to work on aarch64 and start with no path issues
pkgname=firecapture
pkgver=2.7.15
pkgrel=1
pkgdesc="The leading planetary capture tool"
arch=(aarch64)
url="https://www.firecapture.de"
license=('custom:firecapture')
depends=('libusb' 'jdk-openjdk' 'indi-3rdparty-libs')
source=("${pkgname}_${pkgver}_${arch}.deb::https://github.com/devDucks/astroarch-pkgs/raw/refs/heads/main/packages/fire_capture/${pkgname}_${pkgver}_${arch}.deb")
sha512sums=(d453ea00b00f9905e428cad0694bd63f75ccc1bd3b02ed503aed361b22dd03d8b21683377509b74df8d8e828355f0fe114535eb9d1457bf45d4b49e7994b86dd)
_filename=${pkgname}_${pkgver}_${arch}.deb
install=fc.install

prepare() {
	ar p ${_filename} data.tar.xz > data.tar.xz
}

package() {
	echo "  -> Extracting the data.tar.xz..."
	bsdtar -xf data.tar.xz -C "$pkgdir/"
	chmod 755 "$pkgdir/opt"
	chmod 755 "$pkgdir/usr"
	chmod 755 "$pkgdir/usr/share"
	chmod 755 "$pkgdir/usr/share/applications"
	chmod 755 "$pkgdir/opt/FireCapture_v2.7/start.sh"
	chmod -R 755 "$pkgdir/opt/FireCapture_v2.7/jre/bin/"

	#udev rules - demote priority to avoid clashes
	mkdir -p "$pkgdir/etc/udev/"
	cp -R "$pkgdir/lib/udev/rules.d/" "$pkgdir/etc/udev/rules.d/"
	rename 99- 47- $pkgdir/etc/udev/rules.d/*.rules
	chmod -R 755 "$pkgdir/etc/udev/rules.d"

	mv "$pkgdir/lib/" "$pkgdir/usr/lib"
	# chown -R astronaut:astronaut "$pkgdir/opt/FireCapture_v2.7"

	# Tweaks for targeting AstroArch - use system jre & astronaut in default capture directory
	sed -i 's:./jre/bin/java -Xms${heap}m -Xmx${heap}m -classpath "$cp" de.wonderplanets.firecapture.gui.FireCapture:java -Xms${heap}m -Xmx${heap}m -classpath "$cp" de.wonderplanets.firecapture.gui.FireCapture:g' "$pkgdir/opt/FireCapture_v2.7/start.sh"
	sed -i "s:captureRootDir=/home/pi:captureRootDir=/home/astronaut:g" "$pkgdir/opt/FireCapture_v2.7/FireCapture.ini"

	# License
	install -Dm644 "$pkgdir/opt/FireCapture_v2.7/license.txt" "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}
