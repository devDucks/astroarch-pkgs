# Maintainer: Mattia Procopio (astro.matto) <matto.astro at gmail dot com>
pkgname=kstars
pkgver=3.7.9
pkgrel=3
pkgdesc="Kstars planetary software and ekos"
arch=(x86_64 aarch64)
url="https://edu.kde.org/kstars/"
license=('APACHE2' 'BSD3' 'GPL' 'LGPL' 'LGPLv2' 'LGPLv3' 'MIT')
depends=('breeze-icons' 'hicolor-icon-theme' 'knewstuff' 'knotifyconfig' 'kplotting' \
         'libindi>=2.0.8' 'libraw' 'libxisf' 'qt6-datavis3d' 'qt5-quickcontrols' 'qt6-websockets' 'qtkeychain-qt6' \
	 'stellarsolver>=2.0' 'xplanet' 'eigen' 'kdoctools' 'opencv')
makedepends=(cmake extra-cmake-modules)
source=("https://github.com/KDE/kstars/archive/refs/heads/stable-$pkgver.zip")
sha256sums=('91a108f1fbe435b7cd3d78c1fc283d441f59379290a00fd370aaf6d9ff904d0c')

build() {
  cmake -B build -S $pkgname-stable-$pkgver \
	-DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTING=OFF \
        -DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
        -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
