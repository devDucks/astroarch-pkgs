pkgname=siril
pkgver='1.4.0'
pkgrel=1
beta="-beta4"
pkgdesc="An astronomical image processing software for Linux. (IRIS clone)"
arch=('aarch64' 'x86_64')
license=('GPL3')
depends=('gtk3' 'lcms2' 'cfitsio' 'wcslib' 'fftw' 'gsl' 'opencv' 'json-glib' 'ffms2')
makedepends=('meson' 'cmake' 'ninja' 'gtksourceview4' 'libgit2')
optdepends=('gvfs: web access'
            'exiv2: get thumbnails from files'
            'libraw: DSLR RAW files import'
            'libtiff: TIFF import and export'
            'libxisf: XISF format support'
            'libjpeg: JPEG import and export'
            'libjxl: JPEG XL format support'
            'libheif: HEIF format files import'
            'libpng: PNG format support'
            'ffmpeg: avi export support'
            'libgit2: git integration to sync with the official siril-scripts repository')

options=(!lto)

source=("https://free-astro.org/download/siril-${pkgver}${beta}.tar.bz2")
sha256sums=('69ee75745bdef9f2ce50ee646b869422e0b17632f9720844d96a8201fd12bbe5')

build() {
  cd "$srcdir/$pkgname-$pkgver${beta}/"
  meson setup --prefix /usr --buildtype release _build
  ninja -C _build
}

package() {
  cd "$srcdir/$pkgname-$pkgver${beta}/"
  meson install -C _build --destdir "$pkgdir"
}
